Перем ДатаИзмененияФайла;

Функция ИмяТипаПоУмолчанию() Экспорт
	Возврат "ПроверкиПроксиСервера";
КонецФункции
Функция ИмяФайлаПроверокПоУмолчанию() Экспорт
	Возврат СтрШаблон("%1.os", ИмяТипаПоУмолчанию());
КонецФункции
Функция ФайлПроверокИзменился(ПутьКФайлуПроверок) Экспорт
	Файл = Новый Файл(ПутьКФайлуПроверок);
	Результат = Ложь;
	Если Файл.ПолучитьВремяИзменения() <> ДатаИзмененияФайла Тогда
		ДатаИзмененияФайла = Файл.ПолучитьВремяИзменения();
		Файл = Неопределено;
		Результат = Истина;
	КонецЕсли;
	Возврат Результат;
КонецФункции

Функция ПроверитьФайл(ОбъектПроверок) Экспорт
	РефлекторОбъекта = Новый Рефлектор;
	МассивИменНужныхМетодов = Новый Массив;
	МассивИменНужныхМетодов.Добавить("ОбработкаПомещенияВХранилище");
	МассивИменНужныхМетодов.Добавить("ОбработкаИзмененияВерсииХранилища");
	МассивИменНужныхМетодов.Добавить("ПостОбработкаПомещенияВХранилище");
	МассивИменНужныхМетодов.Добавить("ОбработкаСозданияХранилища");
	ЕстьОшибки = Ложь;
	ТаблицаМетодов = РефлекторОбъекта.ПолучитьТаблицуМетодов(ОбъектПроверок);
	Для каждого ИмяНужногоМетода из МассивИменНужныхМетодов Цикл
		НайденнаяСтрока = ТаблицаМетодов.Найти(ИмяНужногоМетода, "Имя");
		Если НайденнаяСтрока = Неопределено Тогда
			ТекстОшибки = СтрШаблон("Не найден метод %1 в файле проверок", ИмяНужногоМетода);
			Сообщить(ТекстОшибки);
			ЕстьОшибки = Истина;
		ИначеЕсли НайденнаяСтрока.Экспорт = Ложь Тогда
			ТекстОшибки = СтрШаблон("Метод %1 в файле проверок должен быть экспортным", ИмяНужногоМетода);
			Сообщить(ТекстОшибки);
			ЕстьОшибки = Истина;
		КонецЕсли;
	КонецЦикла;
	Возврат НЕ ЕстьОшибки;
КонецФункции

Функция Подключить(ПутьКФайлуПроверок = Неопределено) Экспорт
	ИскомыйТип = ИмяТипаПоУмолчанию();
	ИмяФайлаПодключения = "";
	ТекущийКаталог = ТекущийКаталог();
	ОбъектПроверок = Неопределено;
	Если ЗначениеЗаполнено(ПутьКФайлуПроверок) Тогда
		ИмяФайлаПодключения = ПутьКФайлуПроверок;
	Иначе
		ИскомыйФайл = СтрШаблон("%1.os", ИскомыйТип);
		Файлы = НайтиФайлы(ТекущийКаталог, ИскомыйФайл);
		Если ЗначениеЗаполнено(Файлы) Тогда
			Файл = Файлы[0];
			ИмяФайлаПодключения = Файл.ПолноеИмя;
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ИмяФайлаПодключения) Тогда
		Попытка
			ОбъектПроверок = ЗагрузитьСценарий(ИмяФайлаПодключения);
			Файл = Новый Файл(ИмяФайлаПодключения);
			ПутьКФайлуПроверок = ИмяФайлаПодключения;
			ДатаИзмененияФайла = Файл.ПолучитьВремяИзменения();
		Исключение
			Сообщить(СтрШаблон("Не удалось подключить файл %1 как сценарий OneScript", ИмяФайлаПодключения));
			Сообщить(ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
	Иначе
		ТекстОшибки = СтрШаблон("
		|Не найден файл %1
		|Выполните команду oproxy init
		|", ОбъединитьПути(ТекущийКаталог, ИскомыйФайл));
		Сообщить(ТекстОшибки);
		Возврат Неопределено;
	КонецЕсли;
	Если НЕ ПроверитьФайл(ОбъектПроверок) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ОбъектПроверок;
КонецФункции
