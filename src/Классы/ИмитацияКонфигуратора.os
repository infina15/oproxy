#Использовать autumn-logos

&Лог("oproxy")
Перем Лог;

Перем Хост Экспорт;
Перем Порт Экспорт;
Перем Соединение;

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Функция ОткрытьСоединение() Экспорт
	Результат = Истина;
	Попытка
		Соединение = Новый TCPСоединение(Хост, Порт);
		Соединение.ОтправитьДвоичныеДанные(ПолучитьДвоичныеДанныеИзСтроки(""));
	Исключение
		Результат = Ложь;
		Лог.Ошибка("Не удалось установить соединения имитации конфигуратора с имитацией хранилища по причине: " + ОписаниеОшибки());
	КонецПопытки;
	Возврат Результат;
КонецФункции

Функция ПолучитьПриветствие() Экспорт
	ДД = Соединение.ПрочитатьДвоичныеДанные();
	ПриветствиеХранилища = ПолучитьДвоичныеДанныеИзHexСтроки("53F5C61A7B");
	Возврат ДД = ПриветствиеХранилища;
КонецФункции

Функция ОтправитьПроизвольныеДанные(ДД) Экспорт
	Результат = Истина;
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ДД);
	Исключение
		Результат = Ложь;
		Лог.Ошибка("~ОтправитьПроизвольныеДанные: " + ОписаниеОшибки());
	КонецПопытки;
	Возврат Результат;
КонецФункции

Функция ПолучитьОтвет() Экспорт
	Ответ = Неопределено;
	Попытка
		Ответ = Соединение.ПрочитатьДвоичныеДанные();
	Исключение
		Лог.Ошибка("~ПолучитьОтвет: " + ОписаниеОшибки());
	КонецПопытки;
	Возврат Ответ;
КонецФункции

Функция ЗакрытьСоединение() Экспорт
	Результат = Истина;
	Попытка
		Соединение.Закрыть();
	Исключение
		Результат = Ложь;
		Лог.Ошибка("Не удалось закрыть соединение имитации хранилища: " + ОписаниеОшибки());
	КонецПопытки;
	Возврат Результат;
КонецФункции