#Использовать autumn-logos

&Лог("oproxy")
Перем Лог;

&Пластилин Перем ОбработчикСоединений;
//АдресСервера
Перем АдресСервера Экспорт;
//ПортСервера
Перем ПортСервера Экспорт;
//ПортПрокси
Перем ПортПрокси Экспорт;
//ПутьКФайлуПроверок
Перем ПутьКФайлуПроверок Экспорт;
//ТребуетсяОстановка
Перем ТребуетсяОстановка Экспорт;
//ОжиданиеСоединения
Перем ОжиданиеСоединения Экспорт;
//Запущен
Перем Запущен Экспорт;

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры 

Процедура Запустить() Экспорт
	Запущен = Ложь;
	ОбъектПроверок = РаботаСФайломПроверок.Подключить(ПутьКФайлуПроверок);
	Если ОбъектПроверок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Попытка
		Сервер = Новый TCPСервер(ПортПрокси);
		Сервер.Запустить();
		Шаблон = "Прокси-сервер запущен на порту %1 к хранилищу tcp://%2:%3";
		Запущен = Истина;
		Лог.Информация(СтрШаблон(Шаблон, ПортПрокси, АдресСервера, ПортСервера));
	Исключение
		Шаблон =
		"
		|###################################################################################
		| >>> Ошибка запуска прокси-сервера на порту %1 к хранилищу tcp://%2:%3
		| >>> Возможно, порт %1 занят другой программой (или запущенным экземпляром этой)
		|###################################################################################
		|
		|%4";
		Лог.Предупреждение(СтрШаблон(Шаблон, ПортПрокси, АдресСервера, ПортСервера, ОписаниеОшибки()));
		Запущен = Ложь;
	КонецПопытки;
	ОбработчикСоединений.ПроверкиПроксиСервера = ОбъектПроверок;
	ОбработчикСоединений.АдресСервера = АдресСервера;
	ОбработчикСоединений.ПортСервера = ПортСервера;
	ТребуетсяОстановка = Ложь;
	Если ОжиданиеСоединения = Неопределено Тогда
		ОжиданиеСоединения = 1000;
	КонецЕсли;
	СчетчикСоединений = 0;
	Пока НЕ ТребуетсяОстановка Цикл
		Если РаботаСФайломПроверок.ФайлПроверокИзменился(ПутьКФайлуПроверок) Тогда
			ОбъектПроверок = РаботаСФайломПроверок.Подключить(ПутьКФайлуПроверок);
			Если ОбъектПроверок = Неопределено Тогда
				Лог.Предупреждение("Не удалось подключить измененный файл %1, прокси-сервер остановлен.");
				Сервер.Остановить();
				Запущен = Ложь;
				Возврат;
			Иначе
				ОбработчикСоединений.ПроверкиПроксиСервера = ОбъектПроверок;
				Лог.Информация(СтрШаблон("Сценарий файла проверок перезагружен так как был изменен: %1", ПутьКФайлуПроверок));
			КонецЕсли;
		КонецЕсли;
		Соединение = Сервер.ОжидатьСоединения(ОжиданиеСоединения);
		Если Соединение <> Неопределено Тогда
			Лог.Отладка("Новое соединение от " + Соединение.УдаленныйУзел);
			//Соединение.ТаймаутОтправки = 60000;
			//Соединение.ТаймаутЧтения = 60000;
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Соединение);
			МассивПараметров.Добавить(СчетчикСоединений);
			ФоновыеЗадания.Выполнить(ОбработчикСоединений, "ОбработатьСоединение", МассивПараметров);
			ФоновыеЗадания.Очистить();
			СчетчикСоединений = СчетчикСоединений + 1;
		КонецЕсли;
	КонецЦикла;
	Если ТребуетсяОстановка Тогда
		Запущен = Ложь;
		Сервер.Остановить();
		Лог.Информация("Прокси-сервер остановлен");
	КонецЕсли;
КонецПроцедуры
