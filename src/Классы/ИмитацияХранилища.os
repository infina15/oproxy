#Использовать autumn-logos

&Лог("oproxy")
Перем Лог;

Перем Порт Экспорт;
Перем Запущен Экспорт;
Перем ТребуетсяОстановка Экспорт;
Перем ОжиданиеСоединения Экспорт;
Перем ПоследнееПринятоеСообщение;
Перем БлокировкаПоследнегоСообщения;

Перем ПриветствиеХранилища;


&Желудь
Процедура ПриСозданииОбъекта()
	ПриветствиеХранилища = ПолучитьДвоичныеДанныеИзHexСтроки("53F5C61A7B");
	Запущен = Ложь;
	ТребуетсяОстановка = Ложь;
	ОжиданиеСоединения = 1000;
	БлокировкаПоследнегоСообщения = Новый БлокировкаРесурса();
КонецПроцедуры

Процедура Запустить() Экспорт
	Сервер = Новый TCPСервер(Порт);
	Попытка
		Сервер.Запустить();
		Запущен = Истина;
		Лог.Информация("Имитация хранилища запущена на порту " + Порт);
	Исключение
		Лог.Ошибка("Не удалось запустить имитацию хранилища: " + ОписаниеОшибки());
	КонецПопытки;
	Пока НЕ ТребуетсяОстановка Цикл
		Соединение = Сервер.ОжидатьСоединения(ОжиданиеСоединения);
		Если Соединение <> Неопределено Тогда
			МассивПараметров = Новый Массив;
			МассивПараметров.Добавить(Соединение);
			ФоновыеЗадания.Выполнить(ЭтотОбъект, "ОбработатьСоединение", МассивПараметров);
			ФоновыеЗадания.Очистить();
		КонецЕсли;
	КонецЦикла;
	Если ТребуетсяОстановка Тогда
		Запущен = Ложь;
		Сервер.Остановить();
		Лог.Информация("Имитация хранилища остановлена");
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьПоследнееПринятоеСообщение() Экспорт
	БлокировкаПоследнегоСообщения.Заблокировать();
	ДД = ПоследнееПринятоеСообщение;
	БлокировкаПоследнегоСообщения.Разблокировать();
	Возврат ДД;
КонецФункции

Процедура ОбработатьСоединение(Соединение) Экспорт
	//сначала хранилище посылает приветствие
	Попытка
		Соединение.ОтправитьДвоичныеДанные(ПриветствиеХранилища);
		Лог.Информация("Хранилище: приветствие");
		Пока Соединение.Активно Цикл
			ДД = Соединение.ПрочитатьДвоичныеДанные();
			БлокировкаПоследнегоСообщения.Заблокировать();
			ПоследнееПринятоеСообщение = ДД;
			Соединение.ОтправитьДвоичныеДанные(ДД);
			БлокировкаПоследнегоСообщения.Разблокировать();
		КонецЦикла;
	Исключение
		Лог.Информация("Соединение хранилища остановлено: " + ОписаниеОшибки());
		Запущен = Ложь;
	КонецПопытки;

	Попытка
		Соединение.Закрыть();
	Исключение
		Лог.Ошибка("Ошибка во время закрытия соединения имитацией хранилища: " + ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры
